SPACE = _{" "|"\t"}
SPACES = _{SPACE*}

// Tags
TAG_OPEN = _{"<<"}
TAG_CLOSE = _{">>"}

// Keywords
CODE = _{"code"}
PLACEHOLDER = _{"placeholder"}

// Comment and markup definitions
COMMENT_SYMBOLS = _{"#"|"//"}
COMMENT_DEF = _{COMMENT_SYMBOLS}
MARKUP = _{COMMENT_DEF ~ "|"}
REGULAR_COMMENT = _{COMMENT_DEF ~ !"|"}

// Captures any single line string
str = {(!NEWLINE ~ ANY)*}

// Source code elements
comment_space = {SPACES}
source_comment = {comment_space ~ REGULAR_COMMENT ~ SPACES ~ str}
source_code = {(!(NEWLINE|MARKUP) ~ ANY)*}
source_code_block = @{((source_comment | source_code) ~ NEWLINE)+}
source_comment_block = {(source_comment ~ NEWLINE)+}

// Code block definitions
code_block = {SPACES ~ code_block_def ~ NEWLINE ~ solution ~ (code_block_placeholder ~ NEWLINE ~ placeholder)? ~ code_block_end  }
code_block_def = _{ MARKUP ~ SPACES ~ CODE ~ SPACES ~ TAG_OPEN}
code_block_end = _{ SPACES ~ MARKUP ~ SPACES ~ TAG_CLOSE }
code_block_placeholder = _{ SPACES ~ MARKUP ~ SPACES ~ PLACEHOLDER }

// Solution and placeholder definitions
solution = {(source_code_block)*}
placeholder = {(source_comment_block)*}

// Top level glue
top_level = _{SPACES ~ (code_block ~ NEWLINE | source_code_block)}
doc = _{top_level+}